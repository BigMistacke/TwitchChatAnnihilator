?start: rules

rules: config+

config: ["timeout" INT] ["cooldown" INT] ["reason" STRING] "-" condition

condition_list: condition ("," condition)*

?condition: logic_condition
          | contains_condition
          | role_condition
          | bit_condition
          | word_said_condition
          | message_said_condition
          | random_condition
          | regex_condition
          | length_condition
          | emote_count_condition
          | caps_condition
          | starts_with_condition
          | ends_with_condition
          | mention_condition
          | punctuation_condition
          | user_contains_condition
          | user_start_with_condition
          | user_ends_with_condition

// Logic
logic_condition: LOGIC_OP "[" condition_list "]"

# Message text match
contains_condition: "contains" "[" [CONTAINS_OP] [STRING_MATCH] word_list "]"
starts_with_condition: "starts_with" "[" [STRING_MATCH] STRING "]"
ends_with_condition: "ends_with" "[" [STRING_MATCH] STRING "]"
regex_condition: "regex" "[" STRING "]"

# Message text elements
emote_count_condition: "emotes" "[" COMP_OP INT "]"
punctuation_condition: "punctuation" "[" COMP_OP INT "]"
length_condition: "length" "[" COMP_OP INT "]"
caps_condition: "caps" "[" COMP_OP FLOAT "]"
word_said_condition: "said_word" "[" [PERSISTANCE] [STRING_MATCH] [FLOAT] "]"
message_said_condition: "said_message" "[" [PERSISTANCE] [STRING_MATCH] [FLOAT] "]"

//Message attributes
bit_condition: "bits" "[" COMP_OP INT "]"
mention_condition: "mentions" "[" COMP_OP INT "]"

//Username
user_contains_condition: "user_contains" "[" [CONTAINS_OP] [STRING_MATCH] word_list "]"
user_start_with_condition: "user_starts" "[" [STRING_MATCH] STRING "]"
user_ends_with_condition: "user_ends" "[" [STRING_MATCH] STRING "]"
role_condition: "role" "[" ROLE "]"

// Other
random_condition: "random" "[" FLOAT "]"

word_list: string_list | lexicon
string_list: STRING ("," STRING)*
lexicon: "lexicon" STRING


LOGIC_OP: "all" | "notall" | "any" | "none" | "one"
CONTAINS_OP: "all" | "notall" | "any" | "none" | "one" | "only"
//COMPARISON_OP: "contains" | "starts_with" | "ends_with"
COMP_OP: ">" | "<" | "==" | "!="
STRING_MATCH: "strict" | "loose"
ROLE: "VIP" | "moderator" | "sub"
PERSISTANCE: "save" | "no_save"

%import common.INT
%import common.FLOAT
%import common.ESCAPED_STRING -> STRING
%import common.WS
%ignore WS
